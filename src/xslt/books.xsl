<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:exslt="http://exslt.org/common" extension-element-prefixes="exslt">
    <xsl:output method="html" encoding="UTF-8" indent="yes" doctype-system="about:legacy-compat"/>

    <!--
    „Éö„Éº„Ç∏„Éç„Éº„Ç∑„Éß„É≥„ÅÆ„Åü„ÇÅ„ÅÆ„Éë„É©„É°„Éº„Çø„Éº„ÄÇ
    „Å®„Çä„ÅÇ„Åà„Åö 50 ‰ª∂„Åö„Å§„Å´„Åó„Å¶„ÅÇ„Çã„ÄÇ
    -->
    <xsl:param name="page" select="1"/>
    <xsl:param name="per_page" select="50"/>
    <xsl:param name="filter_title" select="''"/>
    <xsl:param name="filter_author" select="''"/>
    <xsl:param name="filter_publisher" select="''"/>
    <xsl:param name="hashes_xml_path"/>

    <!--
    ËëóËÄÖÂêç„ÅØ sha256 „Åß„Éè„ÉÉ„Ç∑„É•Âåñ„Åó„Å¶„ÄÅÂà•„Åß xml „ÅßÁÆ°ÁêÜ„Åó„Å¶„ÅÑ„Çã„ÄÇ
    -->
    <xsl:variable name="author_hashes" select="document($hashes_xml_path)/authors/author"/>

    <xsl:variable name="lowercase" select="'abcdefghijklmnopqrstuvwxyz'"/>
    <xsl:variable name="uppercase" select="'ABCDEFGHIJKLMNOPQRSTUVWXYZ'"/>

    <xsl:template match="/">
        <html lang="ja">
            <head>
                <meta charset="UTF-8"/>
                <title>Êõ∏Á±ç‰∏ÄË¶ß - Êõ∏Á±ç„Ç¢„Éº„Ç´„Ç§„Éñ</title>
                <link rel="stylesheet" href="/css/common.css"/>
                <link rel="stylesheet" href="/css/books.css"/>
            </head>
            <body>
                <div class="container">
                    <div class="header">
                        <h1>üìñ Êõ∏Á±ç‰∏ÄË¶ß</h1>
                        <a href="../index.html" class="back-link">‚Üê „Éõ„Éº„É†„Å´Êàª„Çã</a>
                    </div>

                    <div class="content">
                        <!--
                        Êõ∏Á±ç„É™„Çπ„Éà„Çí„Éï„Ç£„É´„Çø„É™„É≥„Ç∞„Åô„Çã„Åü„ÇÅ„ÅÆÂ§âÊï∞„ÄÇ
                        „Éë„É©„É°„Éº„Çø„ÅåÁ©∫„ÅÆÂ†¥Âêà„ÄÅor Êù°‰ª∂„Åå true „Å®„Å™„Çä„ÄÅÂÆüË≥™ÁöÑ„Å´ÂÖ®‰ª∂ÂèñÂæó„Å®„Å™„Çã„ÄÇ
                        „Åì„Çå„Å´„Çà„Çä„ÄÅÊ§úÁ¥¢Ê©üËÉΩ„ÅåÂÆüË£Ö„Åï„Çå„Å¶„ÅÑ„Å™„ÅÑÁèæÁä∂„Åß„ÇÇ„ÄÅÂÖ®Êõ∏Á±ç„ÇíÂØæË±°„Å®„Åó„Å¶Ê≠£„Åó„ÅèÂãï‰Ωú„Åô„Çã„ÄÇ
                        -->
                        <xsl:variable name="all_books" select="//item[
                            (contains(translate(title, $lowercase, $uppercase), translate($filter_title, $lowercase, $uppercase)) or $filter_title = '') and
                            (contains(translate(creator, $lowercase, $uppercase), translate($filter_author, $lowercase, $uppercase)) or $filter_author = '') and
                            (contains(translate(publisher, $lowercase, $uppercase), translate($filter_publisher, $lowercase, $uppercase)) or $filter_publisher = '')
                        ]"/>

                        <xsl:variable name="total_books" select="count($all_books)"/>
                        <xsl:variable name="total_pages" select="ceiling($total_books div $per_page)"/>
                        <xsl:variable name="start_index" select="($page - 1) * $per_page + 1"/>
                        <xsl:variable name="end_index" select="$page * $per_page"/>

                        <div class="book-count">
                            ÂÖ®
                            <strong>
                                <xsl:value-of select="$total_books"/>
                            </strong>
                            ‰ª∂‰∏≠
                            <strong>
                                <xsl:value-of select="$start_index"/>-
                                <xsl:choose>
                                    <xsl:when test="$end_index > $total_books">
                                        <xsl:value-of select="$total_books"/>
                                    </xsl:when>
                                    <xsl:otherwise>
                                        <xsl:value-of select="$end_index"/>
                                    </xsl:otherwise>
                                </xsl:choose>
                            </strong>
                            ‰ª∂Ë°®Á§∫ („Éö„Éº„Ç∏
                            <strong><xsl:value-of select="$page"/>/<xsl:value-of select="$total_pages"/>
                            </strong>
                            )
                        </div>

                        <!-- Pagination (top) -->
                        <xsl:call-template name="pagination">
                            <xsl:with-param name="page" select="$page"/>
                            <xsl:with-param name="total_pages" select="$total_pages"/>
                        </xsl:call-template>

                        <table>
                            <thead>
                                <tr>

                                    <th>„Çø„Ç§„Éà„É´</th>
                                    <th>ËëóËÄÖ</th>
                                    <th>Âá∫ÁâàÁ§æ</th>
                                    <th>‰æ°Ê†º</th>
                                    <th>ISBN</th>
                                </tr>
                            </thead>
                            <tbody>
                                <xsl:for-each select="$all_books">
                                    <xsl:if test="position() >= $start_index and position() &lt;= $end_index">
                                        <tr>
                                            <td>
                                                <a href="{concat('detail_', normalize-space(isbn), '.html')}">
                                                    <xsl:value-of select="title"/>
                                                </a>
                                            </td>
                                            <td>
                                                <xsl:variable name="author_name" select="normalize-space(creator)"/>
                                                <xsl:variable name="author_name_slug"
                                                              select="translate($author_name, ' ', '+')"/>
                                                <xsl:variable name="author_hash"
                                                              select="$author_hashes[name=$author_name_slug]/hash"/>
                                                <a href="{concat('/authors/', $author_hash, '.html')}"
                                                   class="author-link">
                                                    <xsl:value-of select="creator"/>
                                                </a>
                                            </td>
                                            <td>
                                                <xsl:value-of select="publisher"/>
                                            </td>
                                            <td class="price">¬•<xsl:value-of select="format-number(price, '#,##0')"/>
                                            </td>
                                            <td>
                                                <xsl:value-of select="isbn"/>
                                            </td>
                                        </tr>
                                    </xsl:if>
                                </xsl:for-each>
                            </tbody>
                        </table>

                        <!-- Pagination (bottom) -->
                        <xsl:call-template name="pagination">
                            <xsl:with-param name="page" select="$page"/>
                            <xsl:with-param name="total_pages" select="$total_pages"/>
                        </xsl:call-template>
                    </div>
                </div>
            </body>
        </html>
    </xsl:template>

    <xsl:template name="pagination">
        <xsl:param name="page"/>
        <xsl:param name="total_pages"/>

        <xsl:if test="$total_pages > 1">
            <div class="pagination">
                <xsl:if test="$page > 1">
                    <a href="page_1.html">‚â™ ÊúÄÂàù</a>
                    <a href="page_{$page - 1}.html">‚Äπ Ââç„Å∏</a>
                </xsl:if>
                <div class="page-numbers">
                    <xsl:variable name="start_page">
                        <xsl:choose>
                            <xsl:when test="$page > 5">
                                <xsl:value-of select="$page - 4"/>
                            </xsl:when>
                            <xsl:otherwise>1</xsl:otherwise>
                        </xsl:choose>
                    </xsl:variable>
                    <xsl:variable name="end_page">
                        <xsl:choose>
                            <xsl:when test="$page + 4 > $total_pages">
                                <xsl:value-of select="$total_pages"/>
                            </xsl:when>
                            <xsl:otherwise>
                                <xsl:value-of select="$page + 4"/>
                            </xsl:otherwise>
                        </xsl:choose>
                    </xsl:variable>

                    <xsl:call-template name="page-links">
                        <xsl:with-param name="current" select="$start_page"/>
                        <xsl:with-param name="end" select="$end_page"/>
                        <xsl:with-param name="page" select="$page"/>
                    </xsl:call-template>
                </div>
                <xsl:if test="$page &lt; $total_pages">
                    <a href="page_{$page + 1}.html">Ê¨°„Å∏ ‚Ä∫</a>
                    <a href="page_{$total_pages}.html">ÊúÄÂæå ‚â´</a>
                </xsl:if>
            </div>
        </xsl:if>
    </xsl:template>

    <xsl:template name="page-links">
        <xsl:param name="current"/>
        <xsl:param name="end"/>
        <xsl:param name="page"/>

        <xsl:if test="$current &lt;= $end">
            <xsl:choose>
                <xsl:when test="$current = $page">
                    <span class="current">
                        <xsl:value-of select="$current"/>
                    </span>
                </xsl:when>
                <xsl:otherwise>
                    <a href="page_{$current}.html">
                        <xsl:value-of select="$current"/>
                    </a>
                </xsl:otherwise>
            </xsl:choose>

            <xsl:call-template name="page-links">
                <xsl:with-param name="current" select="$current + 1"/>
                <xsl:with-param name="end" select="$end"/>
                <xsl:with-param name="page" select="$page"/>
            </xsl:call-template>
        </xsl:if>
    </xsl:template>
</xsl:stylesheet>
